buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
        springCloudVersion = 'Dalston.SR1'
        dockerPluginVersion = '3.0.12'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}"

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.+'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

allprojects {
    group = 'info.developerblog.spring.thrift'
    version = '1.5.0'

    repositories {
        jcenter()
    }

    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }

        dependencies {
            dependencySet(group: "org.springframework.boot", version: "${springBootVersion}") {
                entry "spring-boot-starter-web"
                entry "spring-boot-starter-test"
                entry "spring-boot-starter-actuator"
            }

            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'ru.aatarasoff.thrift:api-gateway-core:0.2.1'
            dependency 'org.apache.thrift:libthrift:0.9.3'
            dependency 'info.developerblog.spring.thrift:spring-thrift-starter:1.4.0.RELEASE'
        }
    }



    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}
